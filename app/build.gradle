/*
 * Copyright (C) 2023 Kaleyra S.p.A. All Rights Reserved.
 * See LICENSE.txt for licensing information
 */

plugins {
    id "com.android.application"
    id "kotlin-android"
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.plugin.serialization' version sampleAppCatalog.versions.jetbrainsKotlinVersion
}

apply from: 'versions.gradle'
apply from: '../scripts/apply_dependencies.gradle'

def kaleyraVideoSdkVersion = "4.1.1"

android {
    namespace "com.kaleyra.demo_video_sdk"
    compileSdkVersion sampleAppCatalog.versions.sampleAppSdkVersion.get().toInteger()
    buildToolsVersion sampleAppCatalog.versions.sampleAppToolsVersion.get()

    defaultConfig {
        applicationId "com.kaleyra.demo_video_sdk"
        minSdkVersion sampleAppCatalog.versions.sampleAppMinSdkVersion.get().toInteger()
        targetSdkVersion sampleAppCatalog.versions.sampleAppSdkVersion.get().toInteger()
        versionCode VERSION_CODE.toInteger()
        versionName kaleyraVideoSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            keyAlias "debug"
            keyPassword "123debug"
            storeFile file("keystores/debug.keystore")
            storePassword "123debug"
        }

        release {
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
            storeFile file("keystores/release.keystore")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    ndkVersion "23.1.7779620"
}

dependencies {
    /*** Kaleyra Video SDK ***/
    implementation(platform("com.kaleyra:video-sdk-bom:$kaleyraVideoSdkVersion"))

    implementation project(":video-sdk")

    // define this dependency only if you desire to handle usb camera input in a video call
    implementation "com.kaleyra:video-extension-external-camera"
}
