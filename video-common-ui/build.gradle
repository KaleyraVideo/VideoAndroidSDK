import com.github.jk1.license.render.JsonReportRenderer

/*
 * Copyright 2023 Kaleyra @ https://www.kaleyra.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'maven-publish'
    alias(catalog.plugins.dokka)
    alias(catalog.plugins.licenseReport)
}

apply from: '../sdkAndroid.gradle'
apply from: '../publish.gradle'
apply from: '../licenses.gradle'

android {
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                maxParallelForks = Runtime.runtime.availableProcessors()
                forkEvery = 100
            }
        }
    }
}

dependencies {

    /** Platform **/
    api platform(catalog.androidxComposeBom)
    api 'androidx.compose.material:material'
    api 'androidx.compose.foundation:foundation'
    api 'androidx.compose.runtime:runtime'
    api 'androidx.compose.ui:ui-tooling-preview'
    api 'androidx.lifecycle:lifecycle-viewmodel-compose'
    api 'androidx.lifecycle:lifecycle-runtime-compose'

    api catalog.appcompat
    api catalog.androidXActivityCompose
    api catalog.material
    api catalog.googleAccompanistSystemUiController
    api catalog.googleAccompanistPermission
    implementation 'androidx.test:core-ktx:1.5.0'
    coreLibraryDesugaring catalog.desugar

    /** UI **/
    api catalog.squareupPicasso
    implementation catalog.androidxConstraintLayout

    /** Testing **/
    testImplementation catalog.junit
    testImplementation catalog.mockkAndroid
//    testImplementation catalog.roboelectric
    testImplementation "org.robolectric:robolectric:4.11.1"
    testImplementation catalog.coroutines
    testImplementation catalog.coroutinesTest
//    testImplementation kaleyraCatalog.kaleyraVideoExtensionAudio
    testImplementation project(':video-extension-audio')
    testImplementation kaleyraCatalog.kaleyraVideo

    androidTestImplementation catalog.mockkAndroid
    androidTestImplementation catalog.androidxTestEspressoIntents
    androidTestImplementation catalog.junitTestExt

    /** Kaleyra **/
    implementation kaleyraCatalog.kaleyraVideoUtils
//    compileOnly kaleyraCatalog.kaleyraVideo
    implementation project(':video')
//    compileOnly kaleyraCatalog.kaleyraVideoExtensionAudio
    implementation project(':video-extension-audio')
}

licenseReport {
    // Set output directory for the report data.
    // Defaults to ${project.buildDir}/reports/dependency-license.
    outputDir = "$projectDir/build/licenses"

    // Select projects to examine for dependencies.
    // Defaults to current project and all its subprojects
    projects = [project] + project.subprojects

    // Adjust the configurations to fetch dependencies. Default is 'runtimeClasspath'
    // For Android projects use 'releaseRuntimeClasspath' or 'yourFlavorNameReleaseRuntimeClasspath'
    // Use 'ALL' to dynamically resolve all configurations:
    // configurations = ALL
    configurations = ['releaseRuntimeClasspath']

    // Set custom report renderer, implementing ReportRenderer.
    // Yes, you can write your own to support any format necessary.
    renderers = [new JsonReportRenderer('licenses.json', false)]
}

// workaround for mockk spyk(proxy)
// https://mockk.io/doc/md/jdk16-access-exceptions.html
tasks.withType(Test) {
    jvmArgs(
            "--add-opens", "java.base/java.time=ALL-UNNAMED",
            "--add-opens", "java.base/java.lang.reflect=ALL-UNNAMED"
    )
}
