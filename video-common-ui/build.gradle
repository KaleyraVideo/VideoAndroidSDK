import com.github.jk1.license.render.JsonReportRenderer

/*
 * Copyright 2023 Kaleyra @ https://www.kaleyra.com
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'maven-publish'
    alias(catalog.plugins.dokka)
    alias(catalog.plugins.licenseReport)
}

apply from: '../sdkAndroid.gradle'
apply from: '../publish.gradle'
apply from: '../licenses.gradle'

android {
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                jvmArgs "-Xmx8g"
                setMaxHeapSize('8g')
                maxParallelForks = Runtime.runtime.availableProcessors()
                forkEvery = 100
                testLogging {
                    events "passed", "skipped", "failed", "standardOut", "standardError"
                    outputs.upToDateWhen { false }
                    showStandardStreams = true
                }
                systemProperty 'robolectric.logging.enabled', true
            }
        }
    }
}

dependencies {

    /** Platform **/
    implementation platform(catalog.androidxComposeBom)
    compileOnly 'androidx.compose.material:material'
    compileOnly 'androidx.compose.material3:material3'
    compileOnly 'androidx.compose.foundation:foundation'
    compileOnly 'androidx.compose.runtime:runtime'
    compileOnly 'androidx.compose.ui:ui-tooling-preview'
    compileOnly 'androidx.lifecycle:lifecycle-viewmodel-compose'
    compileOnly 'androidx.lifecycle:lifecycle-runtime-compose'
    compileOnly 'androidx.compose.ui:ui-tooling'
    api kaleyraCatalog.kaleyraMaterialColorUtilities

    api catalog.appcompat
    api catalog.androidXActivityCompose
    api catalog.material
    api catalog.googleAccompanistSystemUiController
    api catalog.googleAccompanistPermission
    coreLibraryDesugaring catalog.desugar

    /** UI **/
    api catalog.squareupPicasso
    api catalog.coilCompose
    implementation catalog.androidxConstraintLayout

    /** Testing **/
    testImplementation catalog.junit
    testImplementation catalog.mockkAndroid
    testImplementation catalog.robolectric
    testImplementation catalog.coroutines
    testImplementation catalog.coroutinesTest
    implementation catalog.androidxTestCoreKtx
    testImplementation catalog.javaAssist
//    testImplementation kaleyraCatalog.kaleyraVideoExtensionAudio
//    testImplementation kaleyraCatalog.kaleyraVideo

    testImplementation project(":video")
    testImplementation project(":video-extension-audio")

    testImplementation platform(catalog.androidxComposeBom)
    testImplementation 'androidx.compose.material3:material3'

    androidTestImplementation catalog.mockkAndroid
    androidTestImplementation catalog.androidxTestEspressoIntents
    androidTestImplementation catalog.junitTestExt

    implementation sampleAppCatalog.androidxWorkRuntime
    implementation sampleAppCatalog.androidxWorkRuntimeKtx
    /** Kaleyra **/
    implementation kaleyraCatalog.kaleyraVideoUtils
//    compileOnly kaleyraCatalog.kaleyraVideo
//    compileOnly kaleyraCatalog.kaleyraVideoExtensionAudio
    compileOnly project(":video")
    compileOnly project(":video-extension-audio")
}

licenseReport {
    // Set output directory for the report data.
    // Defaults to ${project.buildDir}/reports/dependency-license.
    outputDir = "$projectDir/build/licenses"

    // Select projects to examine for dependencies.
    // Defaults to current project and all its subprojects
    projects = [project] + project.subprojects

    // Adjust the configurations to fetch dependencies. Default is 'runtimeClasspath'
    // For Android projects use 'releaseRuntimeClasspath' or 'yourFlavorNameReleaseRuntimeClasspath'
    // Use 'ALL' to dynamically resolve all configurations:
    // configurations = ALL
    configurations = ['releaseRuntimeClasspath']

    // Set custom report renderer, implementing ReportRenderer.
    // Yes, you can write your own to support any format necessary.
    renderers = [new JsonReportRenderer('licenses.json', false)]
}

// workaround for mockk spyk(proxy)
// https://mockk.io/doc/md/jdk16-access-exceptions.html
tasks.withType(Test) {
    jvmArgs(
            "--add-opens", "java.base/java.time=ALL-UNNAMED",
            "--add-opens", "java.base/java.lang.reflect=ALL-UNNAMED",
            "--add-opens=java.base/java.lang=ALL-UNNAMED",
    )
}
